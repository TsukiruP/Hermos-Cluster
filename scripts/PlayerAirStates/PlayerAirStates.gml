function player_is_air_template(_phase)
{
    switch (_phase)
    {
        case PHASE.ENTER:
        {
            // Detach from ground
            player_ground(false);
            break;
        }
        case PHASE.STEP:
        {
            // Accelerate
            if (input_axis_x != 0)
            {
                image_xscale = input_axis_x;
                if (abs(x_speed) < speed_cap or sign(x_speed) != input_axis_x)
                {
                    x_speed += air_acceleration * input_axis_x;
                    if (abs(x_speed) > speed_cap and sign(x_speed) == input_axis_x)
                    {
                        x_speed = speed_cap * input_axis_x;
                    }
                }
            }
            
            // Apply speed limit
            if (abs(x_speed) > speed_limit) x_speed = speed_limit * sign(x_speed);
            
            // Move
            player_move_in_air();
            if (state_changed) exit;
            
            // Land
            if (on_ground) return player_perform(x_speed != 0 ? player_is_running : player_is_standing);
            
            // Apply air resistance
            if (y_speed < 0 and y_speed > -4) x_speed -= x_speed / 32;
            
            // Fall
            if (y_speed < gravity_cap) y_speed = min(y_speed + gravity_force, gravity_cap);
            break;
        }
        case PHASE.EXIT:
        {
            break;
        }
    }
}

function player_is_falling(_phase)
{
    switch (_phase)
    {
        case PHASE.ENTER:
        {
            if (not (aerial_flags & AERIAL_FLAG_PLATFORM))
            {
                // Rise
                y_speed = -dsin(local_direction) * x_speed;
                x_speed *= dcos(local_direction);
            }
            
            // Detach from ground
            player_ground(false);
            
            // Animate
            animation_start("fall", 0, ["roll"]);
            break;
        }
        case PHASE.STEP:
        {
            // Accelerate
            if (input_axis_x != 0)
            {
                image_xscale = input_axis_x;
                if (abs(x_speed) < speed_cap or sign(x_speed) != input_axis_x)
                {
                    x_speed += air_acceleration * input_axis_x;
                    if (abs(x_speed) > speed_cap and sign(x_speed) == input_axis_x)
                    {
                        x_speed = speed_cap * input_axis_x;
                    }
                }
            }
            
            // Apply speed limit
            if (abs(x_speed) > speed_limit) x_speed = speed_limit * sign(x_speed);
            
            // Move
            player_move_in_air();
            if (state_changed) exit;
            
            // Land
            if (on_ground) return player_perform(x_speed != 0 ? player_is_running : player_is_standing);
            
            // Skill
            player_try_air_skill();
            
            // Apply air resistance
            if (y_speed < 0 and y_speed > -4) x_speed -= x_speed / 32;
            
            // Fall
            if (y_speed < gravity_cap) y_speed = min(y_speed + gravity_force, gravity_cap);
            break;
        }
        case PHASE.EXIT:
        {
            aerial_flags &= ~AERIAL_FLAG_PLATFORM;
            break;
        }
    }
}

function player_is_jumping(_phase)
{
    switch (_phase)
    {
        case PHASE.ENTER:
        {
            // Set flags
            jump_cap = true;
            
            // Leap
            var sine = dsin(local_direction);
            var cosine = dcos(local_direction);
            y_speed = -sine * x_speed - cosine * jump_height;
            x_speed = cosine * x_speed - sine * jump_height;
            
            // Detach from ground
            player_ground(false);
            
            // Animate
            animation_start("jump", 0);
            break;
        }
        case PHASE.STEP:
        {
            if (input_axis_x != 0)
            {
                image_xscale = input_axis_x;
                if (abs(x_speed) < speed_cap or sign(x_speed) != input_axis_x)
                {
                    x_speed += air_acceleration * input_axis_x;
                    if (abs(x_speed) > speed_cap and sign(x_speed) == input_axis_x)
                    {
                        x_speed = speed_cap * input_axis_x;
                    }
                }
            }
            
            // Apply speed limit
            if (abs(x_speed) > speed_limit) x_speed = speed_limit * sign(x_speed);
            
            // Move
            player_move_in_air();
            if (state_changed) exit;
            
            // Land
            if (on_ground) return player_perform(x_speed != 0 ? player_is_running : player_is_standing);
            
            // Skill
            player_try_air_skill();
            
            // Apply release height
            var input_jump = (jump_alternate == 2 ? input_button.aux.check : input_button.jump.check);
            if (jump_cap and y_speed < -jump_release and not input_jump) y_speed = -jump_release;
            
            // Apply air resistance
            if (y_speed < 0 and y_speed > -4) x_speed -= x_speed / 32;
            
            // Fall
            if (y_speed < gravity_cap) y_speed = min(y_speed + gravity_force, gravity_cap);
            break;
        }
        case PHASE.EXIT:
        {
            jump_alternate = 0;
            break;
        }
    }
}

function player_is_hurt(_phase)
{
    switch (_phase)
    {
        case PHASE.ENTER:
        {
            // Set flags
            boost_mode = false;
            
            // Detach from ground
            player_ground(false);
            break;
        }
        case PHASE.STEP:
        {
            // Move
            player_move_in_air();
            if (state_changed) exit;
            
            // Land
            if (on_ground) return player_perform(x_speed != 0 ? player_is_running : player_is_standing);
            
            // Fall
            if (y_speed < gravity_cap) y_speed = min(y_speed + gravity_force, gravity_cap);
            break;
        }
        case PHASE.EXIT:
        {
            recovery_time = RECOVERY_DURATION;
            break;
        }
    }
}

function player_is_dead(_phase)
{
    switch (_phase)
    {
        case PHASE.ENTER:
        {
            // Set flags
            state_time = 64;
            boost_mode = false;
            
            // Detach from ground
            player_ground(false);
            
            // Animate
            animation_start("dead");
            
            // Disable pause
            if (player_index == 0)
            {
                with (ctrlStage) pause_allow = false;
            }
            break;
        }
        case PHASE.STEP:
        {
            if (player_index == 0 and --state_time == 0)
            {
                if (LIVES_ENABLED and --global.life_count <= 0)
                {
                    transition_create(room, TRANSITION.GAME_OVER);
                }
                else
                {
                    transition_create(room, TRANSITION.TRY_AGAIN);
                    with (ctrlGame) game_flags |= GAME_FLAG_KEEP_CHARACTERS;
                }
            }
            
            // Move
            var sine = dsin(gravity_direction);
            var cosine = dcos(gravity_direction);
            x += sine * y_speed;
            y += cosine * y_speed;
            
            // Fall
            if (y_speed < gravity_cap) y_speed = min(y_speed + gravity_force, gravity_cap);
            break;
        }
        case PHASE.EXIT:
        {
            recovery_time = RECOVERY_DURATION;
            break;
        }
    }
}

function player_is_aqua_bounding(_phase)
{
    switch (_phase)
    {
        case PHASE.ENTER:
        {
            // Bound
            x_speed /= 2;
            y_speed = 8 * 0.75;
            
            // Animate
            animation_start("roll");
            break;
        }
        case PHASE.STEP:
        {
            // Accelerate
            if (input_axis_x != 0)
            {
                image_xscale = input_axis_x;
                if (abs(x_speed) < speed_cap or sign(x_speed) != input_axis_x)
                {
                    x_speed += air_acceleration * input_axis_x;
                    if (abs(x_speed) > speed_cap and sign(x_speed) == input_axis_x)
                    {
                        x_speed = speed_cap * input_axis_x;
                    }
                }
            }
            
            // Apply speed limit
            if (abs(x_speed) > speed_limit) x_speed = speed_limit * sign(x_speed);
            
            // Move
            player_move_in_air();
            if (state_changed) exit;
            
            // Rebound
            if (on_ground)
            {
                // Set flags
                jump_cap = true;
                jump_alternate++;
                
                // Leap
                var sine = dsin(local_direction);
                var cosine = dcos(local_direction);
                y_speed = -sine * x_speed - cosine * aqua_bound_height;
                x_speed = cosine * x_speed - sine * aqua_bound_height;
                
                // Detach from ground
                player_ground(false);
                
                // Perform
                player_perform(player_is_jumping, false);
                
                // Sound
                audio_play_sfx(sfxAquaBound);
                exit;
            }
            
            // Apply air resistance
            if (y_speed < 0 and y_speed > -4) x_speed -= x_speed / 32;
            
            // Fall
            if (y_speed < gravity_cap) y_speed = min(y_speed + gravity_force, gravity_cap);
            break;
        }
        case PHASE.EXIT:
        {
            // Animate
            with (shield)
            {
                if (anim_core.name == "aqua") anim_core.variant = 3;
            }
            break;
        }
    }
}